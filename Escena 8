class Escena8 {

  // ===== ESTADOS =====
  int estadoJuego = ESTADO_JUEGO;
  int estadoMujer = MUJER_VERDE;

  // Estados de Aurelio
  int estadoAurelio = AUR_QUIETO; // AUR_QUIETO, AUR_MOVIENDO, AUR_PERDIDO

  // ===== POSICIONES =====
  float aurX, aurY;
  float mujerX, mujerY;

  // ===== MOVIMIENTO =====
  float velocidadAur = 15;

  // ===== TEMPORIZADOR MUJER =====
  int tiempoCambioMujer = 0;
  int intervaloMujer = 5000;

  // ===== CONTROL TECLA =====
  boolean espacioPresionado = false;

  // ===== TEMPORIZADORES =====
  int tiempoAurPerdido = 0;
  int duracionAurPerdido = 3000;

  // ===== TEMPORIZADOR VICTORIA =====
  int tiempoInicioVictoria = 0;
  int duracionVictoria = 5000;
  boolean victoriaIniciada = false;

  // ===== PANTALLA NEGRA =====
  boolean pantallaNegra = false;
  int tiempoPantallaNegra = 0;
  int duracionPantallaNegra = 2000; // 2 segundos

  // ===== IMÁGENES =====
  PImage fondo8_1, fondo8_2, aur1, aur2, aur3, mujer1, mujer2, mujer3;
  float fondoOffset1 = 0;
  float fondoOffset2 = 0;

  // ===== AUDIO =====
  AudioPlayer audio8, pillado, disimulo;

  Escena8() {

    aurX = 500;
    aurY = height/2 + 100;

    mujerX = 200;
    mujerY = height/2 + 25;

    tiempoCambioMujer = millis();

    fondo8_1 = loadResizedImage("fondo8_1.png", width + 70);
    fondo8_2 = loadResizedImage("fondo8_2.png", width + 400);
    aur1 = loadResizedImage("aur8_1.png", 350);
    aur2 = loadResizedImage("aur8_2.png", 300);
    aur3 = loadResizedImage("aur8_3.png", 350);
    mujer1 = loadResizedImage("mujer1.png", 550);
    mujer2 = loadResizedImage("mujer2.png", 485);
    mujer3 = loadResizedImage("mujer3.png", 450);

    victoriaIniciada = false;
  }

  void dibujar() {
    imageMode(CENTER);
    image(fondo8_2, width/2 + fondoOffset2, height/2 - 115);
    image(fondo8_1, width/2 + fondoOffset1, height/2);

    switch (estadoJuego) {

      case ESTADO_JUEGO:
        actualizarMujer();
        manejarMovimientoAurelio();
        dibujarMujer();
        dibujarAurelio();
        comprobarVictoria();
        break;

      case ESTADO_GAME_OVER:
        dibujarMujer();
        dibujarAurelio();
        mostrarGameOver();
        break;

      case ESTADO_VICTORIA:
        dibujarMujer();
        dibujarAurelio();
        mostrarVictoria();
        break;
    }
  }

  // ==============================
  void actualizarMujer() {
    if (millis() - tiempoCambioMujer > intervaloMujer) {
      estadoMujer++;
      if (estadoMujer > MUJER_ROJA) estadoMujer = MUJER_VERDE;
      tiempoCambioMujer = millis();
    }
  }

  // ==============================
  void dibujarMujer() {
    if (estadoMujer == MUJER_VERDE) image(mujer1, mujerX + 35, mujerY);
    else if (estadoMujer == MUJER_AMARILLA) image(mujer3, mujerX + 25, mujerY);
    else image(mujer2, mujerX + 35, mujerY);
  }

  // ==============================
  void dibujarAurelio() {
    if (estadoAurelio == AUR_QUIETO) image(aur2, aurX, aurY);
    else if (estadoAurelio == AUR_MOVIENDO) image(aur1, aurX, aurY);
    else if (estadoAurelio == AUR_PERDIDO) image(aur3, aurX, aurY);
  }

  // ==============================
  void manejarMovimientoAurelio() {

    if (estadoAurelio == AUR_PERDIDO) {
      if (millis() - tiempoAurPerdido > duracionAurPerdido) {
        estadoJuego = ESTADO_GAME_OVER;
      }
      return;
    }

    if (espacioPresionado && estadoJuego == ESTADO_JUEGO) {

      if (estadoMujer == MUJER_ROJA) {
        estadoAurelio = AUR_PERDIDO;
        tiempoAurPerdido = millis();
        return;
      }

      estadoAurelio = AUR_MOVIENDO;
      aurX += velocidadAur;

      fondoOffset1 -= 0.1;
      fondoOffset2 -= 0.005;

      fondoOffset1 = max(fondoOffset1, -200);
      fondoOffset2 = max(fondoOffset2, -100);

    } else {
      estadoAurelio = AUR_QUIETO;
    }
  }

  void teclaPulsada() {
    if (key == ' ') espacioPresionado = true;
  }

  void teclaLevantada() {
    if (key == ' ') espacioPresionado = false;
  }

  // ==============================
  void comprobarVictoria() {
    if (aurX + aur2.width/2 + 75 >= width && !victoriaIniciada) {
      victoriaIniciada = true;
      pantallaNegra = true;
      tiempoPantallaNegra = millis(); // iniciar conteo 2s
      estadoJuego = ESTADO_VICTORIA;
    }
  }

  // ==============================
  void mostrarGameOver() {
    background(0);
    textAlign(CENTER, CENTER);
    textSize(40);
    text("INTÉNTALO OTRA VEZ", width/2, height/2);
  }

  // ==============================
  void mostrarVictoria() {
    if (pantallaNegra) {
      background(0); // pantallazo negro
      if (millis() - tiempoPantallaNegra >= duracionPantallaNegra) {
        pantallaNegra = false;
        estadoGeneral = ESCENA9; // cambiar escena tras 2s
      }
    }
  }
}
