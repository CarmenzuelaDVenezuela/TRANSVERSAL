class Escena7 {

  int estadoEscena7 = JUEGO;

  int pw = 50, ph = 100;
  float xBottom, yBottom;
  float speedBottom = 8;

  ArrayList<Enemy> enemies;

  float metaX, metaY;
  boolean metaActiva = false;

  int tiempoAnterior = 0;
  int intervalo;

  boolean gameOver = false;
  int tiempoGameOver = 0;
  int duracionGameOver = 4000;

  boolean jugadorMoviendose = false;

  Animation aurArr;
  Animation aurDer;
  Animation aurIzq;
  Animation aurActual;

  PImage fondo, arb, meta;

  Escena7() {
    iniciar();

    aurArr = new Animation("aurarr", 4);
    aurDer = new Animation("aurder", 4);
    aurIzq = new Animation("aurizq", 4);

    fondo = loadResizedImage("FondoVerde.png", width);
    arb = loadResizedImage("arb.png", width);
    meta = loadImage("meta.png");

    aurActual = aurArr;
  }

  void iniciar() {
    xBottom = width / 2;
    yBottom = height - ph - 10;

    enemies = new ArrayList<Enemy>();
    tiempoAnterior = millis();
    intervalo = int(random(600, 900));

    generarMeta();

    // -------- ENEMIGOS DESDE EL INICIO --------
    for (int i = 0; i < 4; i++) {
      Enemy e = new Enemy();
      e.y = random(0, height - ph);

      if (random(1) < 0.5) {
        e.x = random(-200, -pw);
        e.direccion = 1;
      } else {
        e.x = random(width + pw, width + 200);
        e.direccion = -1;
      }

      enemies.add(e);
    }
  }

  void dibujar() {
    image(fondo, width / 2, height / 2);

    if (estadoEscena7 == GAME_OVER) {
      dibujarGameOver();
      return;
    }

    dibujarJuego();
    image(arb, width / 2, height / 2);
  }

  void dibujarJuego() {

    // -------- SPAWN CONTINUO --------
    if (millis() - tiempoAnterior > intervalo) {
      Enemy e = new Enemy();
      e.y = random(0, height - ph);

      if (random(1) < 0.5) {
        e.x = -pw;
        e.direccion = 1;
      } else {
        e.x = width;
        e.direccion = -1;
      }

      enemies.add(e);
      tiempoAnterior = millis();
      intervalo = int(random(200, 500));
    }

    // -------- ENEMIGOS --------
    for (int i = enemies.size() - 1; i >= 0; i--) {
      Enemy e = enemies.get(i);
      e.update();
      e.display();

      // -------- COLISIÓN AJUSTADA --------
      float margen = 12;

      if (colision(
            e.x + margen,
            e.y + margen,
            pw - margen * 2,
            ph - margen * 2,
            xBottom,
            yBottom,
            pw,
            ph)) {

        estadoEscena7 = GAME_OVER;
        tiempoGameOver = millis();
      }

      if (e.x < -pw || e.x > width) {
        enemies.remove(i);
      }
    }

    aurActual.display(xBottom, yBottom, jugadorMoviendose);

    if (metaActiva) {
      image(meta, metaX, metaY, pw, ph);
    }

    if (metaActiva && colision(metaX, metaY, pw, ph, xBottom, yBottom, pw, ph)) {
      metaActiva = false;
      estadoGeneral = ESCENA7_5;
    }
  }

  void dibujarGameOver() {
    background(0);

    if ((millis() / 250) % 2 == 0) {
      fill(255);
      textAlign(CENTER, CENTER);
      textSize(40);
      text("INTÉNTALO OTRA VEZ", width / 2, height / 2);
    }

    if (millis() - tiempoGameOver > duracionGameOver) {
      exit();
    }
  }

  void teclaPulsada() {

    jugadorMoviendose = false;

    if (keyCode == LEFT) {
      xBottom -= speedBottom;
      aurActual = aurIzq;
      jugadorMoviendose = true;
    }

    if (keyCode == RIGHT) {
      xBottom += speedBottom;
      aurActual = aurDer;
      jugadorMoviendose = true;
    }

    if (keyCode == UP) {
      yBottom -= speedBottom;
      aurActual = aurArr;
      jugadorMoviendose = true;
    }

    xBottom = constrain(xBottom, 0, width - pw);
  }

  void generarMeta() {
    metaX = random(600, 1000);
    metaY = 70;
    metaActiva = true;
  }

  boolean colision(float x1, float y1, float w1, float h1,
                   float x2, float y2, float w2, float h2) {
    return x1 < x2 + w2 &&
           x1 + w1 > x2 &&
           y1 < y2 + h2 &&
           y1 + h1 > y2;
  }

  // ================= ENEMY =================
  class Enemy {
    float x, y;
    int direccion;
    float speed = random(1, 5);

    Animation animDer;
    Animation animIzq;
    Animation animActual;

    Enemy() {
      String tipo;
      int r = int(random(3));
      if (r == 0) tipo = "a";
      else if (r == 1) tipo = "o";
      else tipo = "v";

      animDer = new Animation(tipo + "der", 4);
      animIzq = new Animation(tipo + "izq", 4);
    }

    void update() {
      x += speed * direccion;

      if (direccion == 1) {
        animActual = animDer;
      } else {
        animActual = animIzq;
      }
    }

    void display() {
      animActual.display(x, y, true);
    }
  }

  // ================= ANIMATION =================
  class Animation {
    PImage[] images;
    int imageCount;
    int frame;
    int lastTime = 0;
    int frameDelay = 166;

    Animation(String imagePrefix, int count) {
      imageCount = count;
      images = new PImage[imageCount];

      for (int i = 0; i < imageCount; i++) {
        String filename = imagePrefix + nf(i, 4) + ".png";
        images[i] = loadImage(filename);
      }
    }

    void display(float xpos, float ypos, boolean animar) {
      if (animar && millis() - lastTime > frameDelay) {
        frame = (frame + 1) % imageCount;
        lastTime = millis();
      }

      image(images[frame], xpos, ypos, pw, ph);
    }
  }
}
